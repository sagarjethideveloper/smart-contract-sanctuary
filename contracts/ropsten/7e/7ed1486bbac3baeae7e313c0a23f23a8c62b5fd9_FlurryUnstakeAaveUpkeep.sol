//SPDX-License-Identifier: MIT
pragma solidity 0.8.4;

import "@openzeppelin/contracts-upgradeable/access/OwnableUpgradeable.sol";
import "../interfaces/IFlurryUpkeep.sol";

interface IAaveV2Strategy {
    function emissionPerSecond() external view returns (uint256);

    function shouldActivateCD() external view returns (bool);

    function shouldUnstake() external view returns (bool);

    function claimStkAave() external;

    function activateCD() external;

    function claimAaveAndUnstake() external;
}

contract FlurryUnstakeAaveUpkeep is OwnableUpgradeable, IFlurryUpkeep {
    uint256 public unstakeInterval; // Daily unstake interval with 1 = 1 second
    uint256 public lastTimeStamp;

    IAaveV2Strategy[] public strategies;
    mapping(address => bool) public strategyRegistered;

    /**
     * @dev there are 3 states for stkAAVE interaction
     * 1. NO_ACTION_NEEDED (default: no stkAAVE emission / stkAAVE locked)
     * 2. PENDING_ACTIVATE_CD (never activated cooldown / unstake window expired / just unstaked)
     * 3. PENDING_UNSTAKE (within unstake window)
     */
    enum StakingState {NO_ACTION_NEEDED, PENDING_ACTIVATE_CD, PENDING_UNSTAKE}

    function initialize(uint256 interval) public initializer {
        OwnableUpgradeable.__Ownable_init();
        unstakeInterval = interval;
        lastTimeStamp = block.timestamp;
    }

    function checkUpkeep(bytes calldata checkData)
        external
        view
        override
        returns (bool upkeepNeeded, bytes memory performData)
    {
        // unstake interval checking and setting
        if ((block.timestamp - lastTimeStamp) < unstakeInterval) return (false, checkData);
        // check stkAave status
        StakingState[] memory stkAaveStates = new StakingState[](strategies.length);
        for (uint256 i; i < strategies.length; i++) {
            if (strategies[i].emissionPerSecond() == 0) continue;
            if (strategies[i].shouldActivateCD()) {
                stkAaveStates[i] = StakingState.PENDING_ACTIVATE_CD;
            } else if (strategies[i].shouldUnstake()) {
                stkAaveStates[i] = StakingState.PENDING_UNSTAKE;
            } else {
                continue; // stkAAVE still in cooldown => NO_ACTION_NEEDED
            }
            upkeepNeeded = true;
        }
        performData = abi.encode(stkAaveStates);
    }

    function performUpkeep(bytes calldata performData) external override {
        lastTimeStamp = block.timestamp;
        StakingState[] memory stkAaveStates = abi.decode(performData, (StakingState[]));
        for (uint256 i = 0; i < strategies.length; i++) {
            // do nothing if stkAAVE is `NO_ACTION_NEEDED`
            if (stkAaveStates[i] == StakingState.PENDING_UNSTAKE) {
                strategies[i].claimAaveAndUnstake();
                stkAaveStates[i] = StakingState.PENDING_ACTIVATE_CD;
            }
            if (stkAaveStates[i] == StakingState.PENDING_ACTIVATE_CD) {
                strategies[i].claimStkAave();
                strategies[i].activateCD();
            }
        }
    }

    function setUnstakeInterval(uint256 interval) external onlyOwner {
        unstakeInterval = interval;
    }

    function registerAaveV2Strategy(address strategyAddr) external onlyOwner {
        require(strategyAddr != address(0), "Strategy address is 0");
        require(!strategyRegistered[strategyAddr], "Strategy already registered");
        strategies.push(IAaveV2Strategy(strategyAddr));
        strategyRegistered[strategyAddr] = true;
    }
}

// SPDX-License-Identifier: MIT

pragma solidity ^0.8.0;

import "../utils/ContextUpgradeable.sol";
import "../proxy/utils/Initializable.sol";
/**
 * @dev Contract module which provides a basic access control mechanism, where
 * there is an account (an owner) that can be granted exclusive access to
 * specific functions.
 *
 * By default, the owner account will be the one that deploys the contract. This
 * can later be changed with {transferOwnership}.
 *
 * This module is used through inheritance. It will make available the modifier
 * `onlyOwner`, which can be applied to your functions to restrict their use to
 * the owner.
 */
abstract contract OwnableUpgradeable is Initializable, ContextUpgradeable {
    address private _owner;

    event OwnershipTransferred(address indexed previousOwner, address indexed newOwner);

    /**
     * @dev Initializes the contract setting the deployer as the initial owner.
     */
    function __Ownable_init() internal initializer {
        __Context_init_unchained();
        __Ownable_init_unchained();
    }

    function __Ownable_init_unchained() internal initializer {
        address msgSender = _msgSender();
        _owner = msgSender;
        emit OwnershipTransferred(address(0), msgSender);
    }

    /**
     * @dev Returns the address of the current owner.
     */
    function owner() public view virtual returns (address) {
        return _owner;
    }

    /**
     * @dev Throws if called by any account other than the owner.
     */
    modifier onlyOwner() {
        require(owner() == _msgSender(), "Ownable: caller is not the owner");
        _;
    }

    /**
     * @dev Leaves the contract without owner. It will not be possible to call
     * `onlyOwner` functions anymore. Can only be called by the current owner.
     *
     * NOTE: Renouncing ownership will leave the contract without an owner,
     * thereby removing any functionality that is only available to the owner.
     */
    function renounceOwnership() public virtual onlyOwner {
        emit OwnershipTransferred(_owner, address(0));
        _owner = address(0);
    }

    /**
     * @dev Transfers ownership of the contract to a new account (`newOwner`).
     * Can only be called by the current owner.
     */
    function transferOwnership(address newOwner) public virtual onlyOwner {
        require(newOwner != address(0), "Ownable: new owner is the zero address");
        emit OwnershipTransferred(_owner, newOwner);
        _owner = newOwner;
    }
    uint256[49] private __gap;
}

//SPDX-License-Identifier: MIT
pragma solidity 0.8.4;

interface IFlurryUpkeep {
    /**
     * @dev checkUpkeep compatible.
     * Return upkeepNeeded (in bool) and performData (in bytes) and untilKeepNeeded (in uint).
     */
    function checkUpkeep(bytes calldata checkData) external returns (bool upkeepNeeded, bytes memory performData);

    /**
     * @dev performUpkeep compatible.
     */
    function performUpkeep(bytes calldata performData) external;
}

// SPDX-License-Identifier: MIT

pragma solidity ^0.8.0;
import "../proxy/utils/Initializable.sol";

/*
 * @dev Provides information about the current execution context, including the
 * sender of the transaction and its data. While these are generally available
 * via msg.sender and msg.data, they should not be accessed in such a direct
 * manner, since when dealing with meta-transactions the account sending and
 * paying for execution may not be the actual sender (as far as an application
 * is concerned).
 *
 * This contract is only required for intermediate, library-like contracts.
 */
abstract contract ContextUpgradeable is Initializable {
    function __Context_init() internal initializer {
        __Context_init_unchained();
    }

    function __Context_init_unchained() internal initializer {
    }
    function _msgSender() internal view virtual returns (address) {
        return msg.sender;
    }

    function _msgData() internal view virtual returns (bytes calldata) {
        this; // silence state mutability warning without generating bytecode - see https://github.com/ethereum/solidity/issues/2691
        return msg.data;
    }
    uint256[50] private __gap;
}

// SPDX-License-Identifier: MIT

// solhint-disable-next-line compiler-version
pragma solidity ^0.8.0;

/**
 * @dev This is a base contract to aid in writing upgradeable contracts, or any kind of contract that will be deployed
 * behind a proxy. Since a proxied contract can't have a constructor, it's common to move constructor logic to an
 * external initializer function, usually called `initialize`. It then becomes necessary to protect this initializer
 * function so it can only be called once. The {initializer} modifier provided by this contract will have this effect.
 *
 * TIP: To avoid leaving the proxy in an uninitialized state, the initializer function should be called as early as
 * possible by providing the encoded function call as the `_data` argument to {ERC1967Proxy-constructor}.
 *
 * CAUTION: When used with inheritance, manual care must be taken to not invoke a parent initializer twice, or to ensure
 * that all initializers are idempotent. This is not verified automatically as constructors are by Solidity.
 */
abstract contract Initializable {

    /**
     * @dev Indicates that the contract has been initialized.
     */
    bool private _initialized;

    /**
     * @dev Indicates that the contract is in the process of being initialized.
     */
    bool private _initializing;

    /**
     * @dev Modifier to protect an initializer function from being invoked twice.
     */
    modifier initializer() {
        require(_initializing || !_initialized, "Initializable: contract is already initialized");

        bool isTopLevelCall = !_initializing;
        if (isTopLevelCall) {
            _initializing = true;
            _initialized = true;
        }

        _;

        if (isTopLevelCall) {
            _initializing = false;
        }
    }
}

