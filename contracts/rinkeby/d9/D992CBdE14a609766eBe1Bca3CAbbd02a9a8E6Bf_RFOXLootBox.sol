// SPDX-License-Identifier: MIT
pragma solidity ^0.7.0;

import "./vendor/SafeMathChainlink.sol";

import "./interfaces/LinkTokenInterface.sol";

import "./VRFRequestIDBase.sol";

/** ****************************************************************************
 * @notice Interface for contracts using VRF randomness
 * *****************************************************************************
 * @dev PURPOSE
 *
 * @dev Reggie the Random Oracle (not his real job) wants to provide randomness
 * @dev to Vera the verifier in such a way that Vera can be sure he's not
 * @dev making his output up to suit himself. Reggie provides Vera a public key
 * @dev to which he knows the secret key. Each time Vera provides a seed to
 * @dev Reggie, he gives back a value which is computed completely
 * @dev deterministically from the seed and the secret key.
 *
 * @dev Reggie provides a proof by which Vera can verify that the output was
 * @dev correctly computed once Reggie tells it to her, but without that proof,
 * @dev the output is indistinguishable to her from a uniform random sample
 * @dev from the output space.
 *
 * @dev The purpose of this contract is to make it easy for unrelated contracts
 * @dev to talk to Vera the verifier about the work Reggie is doing, to provide
 * @dev simple access to a verifiable source of randomness.
 * *****************************************************************************
 * @dev USAGE
 *
 * @dev Calling contracts must inherit from VRFConsumerBase, and can
 * @dev initialize VRFConsumerBase's attributes in their constructor as
 * @dev shown:
 *
 * @dev   contract VRFConsumer {
 * @dev     constuctor(<other arguments>, address _vrfCoordinator, address _link)
 * @dev       VRFConsumerBase(_vrfCoordinator, _link) public {
 * @dev         <initialization with other arguments goes here>
 * @dev       }
 * @dev   }
 *
 * @dev The oracle will have given you an ID for the VRF keypair they have
 * @dev committed to (let's call it keyHash), and have told you the minimum LINK
 * @dev price for VRF service. Make sure your contract has sufficient LINK, and
 * @dev call requestRandomness(keyHash, fee, seed), where seed is the input you
 * @dev want to generate randomness from.
 *
 * @dev Once the VRFCoordinator has received and validated the oracle's response
 * @dev to your request, it will call your contract's fulfillRandomness method.
 *
 * @dev The randomness argument to fulfillRandomness is the actual random value
 * @dev generated from your seed.
 *
 * @dev The requestId argument is generated from the keyHash and the seed by
 * @dev makeRequestId(keyHash, seed). If your contract could have concurrent
 * @dev requests open, you can use the requestId to track which seed is
 * @dev associated with which randomness. See VRFRequestIDBase.sol for more
 * @dev details. (See "SECURITY CONSIDERATIONS" for principles to keep in mind,
 * @dev if your contract could have multiple requests in flight simultaneously.)
 *
 * @dev Colliding `requestId`s are cryptographically impossible as long as seeds
 * @dev differ. (Which is critical to making unpredictable randomness! See the
 * @dev next section.)
 *
 * *****************************************************************************
 * @dev SECURITY CONSIDERATIONS
 *
 * @dev A method with the ability to call your fulfillRandomness method directly
 * @dev could spoof a VRF response with any random value, so it's critical that
 * @dev it cannot be directly called by anything other than this base contract
 * @dev (specifically, by the VRFConsumerBase.rawFulfillRandomness method).
 *
 * @dev For your users to trust that your contract's random behavior is free
 * @dev from malicious interference, it's best if you can write it so that all
 * @dev behaviors implied by a VRF response are executed *during* your
 * @dev fulfillRandomness method. If your contract must store the response (or
 * @dev anything derived from it) and use it later, you must ensure that any
 * @dev user-significant behavior which depends on that stored value cannot be
 * @dev manipulated by a subsequent VRF request.
 *
 * @dev Similarly, both miners and the VRF oracle itself have some influence
 * @dev over the order in which VRF responses appear on the blockchain, so if
 * @dev your contract could have multiple VRF requests in flight simultaneously,
 * @dev you must ensure that the order in which the VRF responses arrive cannot
 * @dev be used to manipulate your contract's user-significant behavior.
 *
 * @dev Since the ultimate input to the VRF is mixed with the block hash of the
 * @dev block in which the request is made, user-provided seeds have no impact
 * @dev on its economic security properties. They are only included for API
 * @dev compatability with previous versions of this contract.
 *
 * @dev Since the block hash of the block which contains the requestRandomness
 * @dev call is mixed into the input to the VRF *last*, a sufficiently powerful
 * @dev miner could, in principle, fork the blockchain to evict the block
 * @dev containing the request, forcing the request to be included in a
 * @dev different block with a different hash, and therefore a different input
 * @dev to the VRF. However, such an attack would incur a substantial economic
 * @dev cost. This cost scales with the number of blocks the VRF oracle waits
 * @dev until it calls responds to a request.
 */
abstract contract VRFConsumerBase is VRFRequestIDBase {

  using SafeMathChainlink for uint256;

  /**
   * @notice fulfillRandomness handles the VRF response. Your contract must
   * @notice implement it. See "SECURITY CONSIDERATIONS" above for important
   * @notice principles to keep in mind when implementing your fulfillRandomness
   * @notice method.
   *
   * @dev VRFConsumerBase expects its subcontracts to have a method with this
   * @dev signature, and will call it once it has verified the proof
   * @dev associated with the randomness. (It is triggered via a call to
   * @dev rawFulfillRandomness, below.)
   *
   * @param requestId The Id initially returned by requestRandomness
   * @param randomness the VRF output
   */
  function fulfillRandomness(
    bytes32 requestId,
    uint256 randomness
  )
    internal
    virtual;

  /**
   * @dev In order to keep backwards compatibility we have kept the user
   * seed field around. We remove the use of it because given that the blockhash
   * enters later, it overrides whatever randomness the used seed provides.
   * Given that it adds no security, and can easily lead to misunderstandings,
   * we have removed it from usage and can now provide a simpler API.
   */
  uint256 constant private USER_SEED_PLACEHOLDER = 0;

  /**
   * @notice requestRandomness initiates a request for VRF output given _seed
   *
   * @dev The fulfillRandomness method receives the output, once it's provided
   * @dev by the Oracle, and verified by the vrfCoordinator.
   *
   * @dev The _keyHash must already be registered with the VRFCoordinator, and
   * @dev the _fee must exceed the fee specified during registration of the
   * @dev _keyHash.
   *
   * @dev The _seed parameter is vestigial, and is kept only for API
   * @dev compatibility with older versions. It can't *hurt* to mix in some of
   * @dev your own randomness, here, but it's not necessary because the VRF
   * @dev oracle will mix the hash of the block containing your request into the
   * @dev VRF seed it ultimately uses.
   *
   * @param _keyHash ID of public key against which randomness is generated
   * @param _fee The amount of LINK to send with the request
   *
   * @return requestId unique ID for this request
   *
   * @dev The returned requestId can be used to distinguish responses to
   * @dev concurrent requests. It is passed as the first argument to
   * @dev fulfillRandomness.
   */
  function requestRandomness(
    bytes32 _keyHash,
    uint256 _fee
  )
    internal
    returns (
      bytes32 requestId
    )
  {
    LINK.transferAndCall(vrfCoordinator, _fee, abi.encode(_keyHash, USER_SEED_PLACEHOLDER));
    // This is the seed passed to VRFCoordinator. The oracle will mix this with
    // the hash of the block containing this request to obtain the seed/input
    // which is finally passed to the VRF cryptographic machinery.
    uint256 vRFSeed  = makeVRFInputSeed(_keyHash, USER_SEED_PLACEHOLDER, address(this), nonces[_keyHash]);
    // nonces[_keyHash] must stay in sync with
    // VRFCoordinator.nonces[_keyHash][this], which was incremented by the above
    // successful LINK.transferAndCall (in VRFCoordinator.randomnessRequest).
    // This provides protection against the user repeating their input seed,
    // which would result in a predictable/duplicate output, if multiple such
    // requests appeared in the same block.
    nonces[_keyHash] = nonces[_keyHash].add(1);
    return makeRequestId(_keyHash, vRFSeed);
  }

  LinkTokenInterface immutable internal LINK;
  address immutable private vrfCoordinator;

  // Nonces for each VRF key from which randomness has been requested.
  //
  // Must stay in sync with VRFCoordinator[_keyHash][this]
  mapping(bytes32 /* keyHash */ => uint256 /* nonce */) private nonces;

  /**
   * @param _vrfCoordinator address of VRFCoordinator contract
   * @param _link address of LINK token contract
   *
   * @dev https://docs.chain.link/docs/link-token-contracts
   */
  constructor(
    address _vrfCoordinator,
    address _link
  ) {
    vrfCoordinator = _vrfCoordinator;
    LINK = LinkTokenInterface(_link);
  }

  // rawFulfillRandomness is called by VRFCoordinator when it receives a valid VRF
  // proof. rawFulfillRandomness then calls fulfillRandomness, after validating
  // the origin of the call
  function rawFulfillRandomness(
    bytes32 requestId,
    uint256 randomness
  )
    external
  {
    require(msg.sender == vrfCoordinator, "Only VRFCoordinator can fulfill");
    fulfillRandomness(requestId, randomness);
  }
}

// SPDX-License-Identifier: MIT
pragma solidity ^0.7.0;

contract VRFRequestIDBase {

  /**
   * @notice returns the seed which is actually input to the VRF coordinator
   *
   * @dev To prevent repetition of VRF output due to repetition of the
   * @dev user-supplied seed, that seed is combined in a hash with the
   * @dev user-specific nonce, and the address of the consuming contract. The
   * @dev risk of repetition is mostly mitigated by inclusion of a blockhash in
   * @dev the final seed, but the nonce does protect against repetition in
   * @dev requests which are included in a single block.
   *
   * @param _userSeed VRF seed input provided by user
   * @param _requester Address of the requesting contract
   * @param _nonce User-specific nonce at the time of the request
   */
  function makeVRFInputSeed(
    bytes32 _keyHash,
    uint256 _userSeed,
    address _requester,
    uint256 _nonce
  )
    internal
    pure
    returns (
      uint256
    )
  {
    return uint256(keccak256(abi.encode(_keyHash, _userSeed, _requester, _nonce)));
  }

  /**
   * @notice Returns the id for this request
   * @param _keyHash The serviceAgreement ID to be used for this request
   * @param _vRFInputSeed The seed to be passed directly to the VRF
   * @return The id for this request
   *
   * @dev Note that _vRFInputSeed is not the seed passed by the consuming
   * @dev contract, but the one generated by makeVRFInputSeed
   */
  function makeRequestId(
    bytes32 _keyHash,
    uint256 _vRFInputSeed
  )
    internal
    pure
    returns (
      bytes32
    )
  {
    return keccak256(abi.encodePacked(_keyHash, _vRFInputSeed));
  }
}

// SPDX-License-Identifier: MIT
pragma solidity ^0.7.0;

interface LinkTokenInterface {

  function allowance(
    address owner,
    address spender
  )
    external
    view
    returns (
      uint256 remaining
    );

  function approve(
    address spender,
    uint256 value
  )
    external
    returns (
      bool success
    );

  function balanceOf(
    address owner
  )
    external
    view
    returns (
      uint256 balance
    );

  function decimals()
    external
    view
    returns (
      uint8 decimalPlaces
    );

  function decreaseApproval(
    address spender,
    uint256 addedValue
  )
    external
    returns (
      bool success
    );

  function increaseApproval(
    address spender,
    uint256 subtractedValue
  ) external;

  function name()
    external
    view
    returns (
      string memory tokenName
    );

  function symbol()
    external
    view
    returns (
      string memory tokenSymbol
    );

  function totalSupply()
    external
    view
    returns (
      uint256 totalTokensIssued
    );

  function transfer(
    address to,
    uint256 value
  )
    external
    returns (
      bool success
    );

  function transferAndCall(
    address to,
    uint256 value,
    bytes calldata data
  )
    external
    returns (
      bool success
    );

  function transferFrom(
    address from,
    address to,
    uint256 value
  )
    external
    returns (
      bool success
    );

}

// SPDX-License-Identifier: MIT
pragma solidity ^0.7.0;

/**
 * @dev Wrappers over Solidity's arithmetic operations with added overflow
 * checks.
 *
 * Arithmetic operations in Solidity wrap on overflow. This can easily result
 * in bugs, because programmers usually assume that an overflow raises an
 * error, which is the standard behavior in high level programming languages.
 * `SafeMath` restores this intuition by reverting the transaction when an
 * operation overflows.
 *
 * Using this library instead of the unchecked operations eliminates an entire
 * class of bugs, so it's recommended to use it always.
 */
library SafeMathChainlink {
  /**
    * @dev Returns the addition of two unsigned integers, reverting on
    * overflow.
    *
    * Counterpart to Solidity's `+` operator.
    *
    * Requirements:
    * - Addition cannot overflow.
    */
  function add(
    uint256 a,
    uint256 b
  )
    internal
    pure
    returns (
      uint256
    )
  {
    uint256 c = a + b;
    require(c >= a, "SafeMath: addition overflow");

    return c;
  }

  /**
    * @dev Returns the subtraction of two unsigned integers, reverting on
    * overflow (when the result is negative).
    *
    * Counterpart to Solidity's `-` operator.
    *
    * Requirements:
    * - Subtraction cannot overflow.
    */
  function sub(
    uint256 a,
    uint256 b
  )
    internal
    pure
    returns (
      uint256
    )
  {
    require(b <= a, "SafeMath: subtraction overflow");
    uint256 c = a - b;

    return c;
  }

  /**
    * @dev Returns the multiplication of two unsigned integers, reverting on
    * overflow.
    *
    * Counterpart to Solidity's `*` operator.
    *
    * Requirements:
    * - Multiplication cannot overflow.
    */
  function mul(
    uint256 a,
    uint256 b
  )
    internal
    pure
    returns (
      uint256
    )
  {
    // Gas optimization: this is cheaper than requiring 'a' not being zero, but the
    // benefit is lost if 'b' is also tested.
    // See: https://github.com/OpenZeppelin/openzeppelin-solidity/pull/522
    if (a == 0) {
      return 0;
    }

    uint256 c = a * b;
    require(c / a == b, "SafeMath: multiplication overflow");

    return c;
  }

  /**
    * @dev Returns the integer division of two unsigned integers. Reverts on
    * division by zero. The result is rounded towards zero.
    *
    * Counterpart to Solidity's `/` operator. Note: this function uses a
    * `revert` opcode (which leaves remaining gas untouched) while Solidity
    * uses an invalid opcode to revert (consuming all remaining gas).
    *
    * Requirements:
    * - The divisor cannot be zero.
    */
  function div(
    uint256 a,
    uint256 b
  )
    internal
    pure
    returns (
      uint256
    )
  {
    // Solidity only automatically asserts when dividing by 0
    require(b > 0, "SafeMath: division by zero");
    uint256 c = a / b;
    // assert(a == b * c + a % b); // There is no case in which this doesn't hold

    return c;
  }

  /**
    * @dev Returns the remainder of dividing two unsigned integers. (unsigned integer modulo),
    * Reverts when dividing by zero.
    *
    * Counterpart to Solidity's `%` operator. This function uses a `revert`
    * opcode (which leaves remaining gas untouched) while Solidity uses an
    * invalid opcode to revert (consuming all remaining gas).
    *
    * Requirements:
    * - The divisor cannot be zero.
    */
  function mod(
    uint256 a,
    uint256 b
  )
    internal
    pure
    returns (
      uint256
    )
  {
    require(b != 0, "SafeMath: modulo by zero");
    return a % b;
  }
}

// SPDX-License-Identifier: MIT

pragma solidity >=0.6.0 <0.8.0;

import "../utils/Context.sol";
/**
 * @dev Contract module which provides a basic access control mechanism, where
 * there is an account (an owner) that can be granted exclusive access to
 * specific functions.
 *
 * By default, the owner account will be the one that deploys the contract. This
 * can later be changed with {transferOwnership}.
 *
 * This module is used through inheritance. It will make available the modifier
 * `onlyOwner`, which can be applied to your functions to restrict their use to
 * the owner.
 */
abstract contract Ownable is Context {
    address private _owner;

    event OwnershipTransferred(address indexed previousOwner, address indexed newOwner);

    /**
     * @dev Initializes the contract setting the deployer as the initial owner.
     */
    constructor () internal {
        address msgSender = _msgSender();
        _owner = msgSender;
        emit OwnershipTransferred(address(0), msgSender);
    }

    /**
     * @dev Returns the address of the current owner.
     */
    function owner() public view virtual returns (address) {
        return _owner;
    }

    /**
     * @dev Throws if called by any account other than the owner.
     */
    modifier onlyOwner() {
        require(owner() == _msgSender(), "Ownable: caller is not the owner");
        _;
    }

    /**
     * @dev Leaves the contract without owner. It will not be possible to call
     * `onlyOwner` functions anymore. Can only be called by the current owner.
     *
     * NOTE: Renouncing ownership will leave the contract without an owner,
     * thereby removing any functionality that is only available to the owner.
     */
    function renounceOwnership() public virtual onlyOwner {
        emit OwnershipTransferred(_owner, address(0));
        _owner = address(0);
    }

    /**
     * @dev Transfers ownership of the contract to a new account (`newOwner`).
     * Can only be called by the current owner.
     */
    function transferOwnership(address newOwner) public virtual onlyOwner {
        require(newOwner != address(0), "Ownable: new owner is the zero address");
        emit OwnershipTransferred(_owner, newOwner);
        _owner = newOwner;
    }
}

// SPDX-License-Identifier: MIT

pragma solidity >=0.6.0 <0.8.0;

/**
 * @dev Wrappers over Solidity's arithmetic operations with added overflow
 * checks.
 *
 * Arithmetic operations in Solidity wrap on overflow. This can easily result
 * in bugs, because programmers usually assume that an overflow raises an
 * error, which is the standard behavior in high level programming languages.
 * `SafeMath` restores this intuition by reverting the transaction when an
 * operation overflows.
 *
 * Using this library instead of the unchecked operations eliminates an entire
 * class of bugs, so it's recommended to use it always.
 */
library SafeMath {
    /**
     * @dev Returns the addition of two unsigned integers, with an overflow flag.
     *
     * _Available since v3.4._
     */
    function tryAdd(uint256 a, uint256 b) internal pure returns (bool, uint256) {
        uint256 c = a + b;
        if (c < a) return (false, 0);
        return (true, c);
    }

    /**
     * @dev Returns the substraction of two unsigned integers, with an overflow flag.
     *
     * _Available since v3.4._
     */
    function trySub(uint256 a, uint256 b) internal pure returns (bool, uint256) {
        if (b > a) return (false, 0);
        return (true, a - b);
    }

    /**
     * @dev Returns the multiplication of two unsigned integers, with an overflow flag.
     *
     * _Available since v3.4._
     */
    function tryMul(uint256 a, uint256 b) internal pure returns (bool, uint256) {
        // Gas optimization: this is cheaper than requiring 'a' not being zero, but the
        // benefit is lost if 'b' is also tested.
        // See: https://github.com/OpenZeppelin/openzeppelin-contracts/pull/522
        if (a == 0) return (true, 0);
        uint256 c = a * b;
        if (c / a != b) return (false, 0);
        return (true, c);
    }

    /**
     * @dev Returns the division of two unsigned integers, with a division by zero flag.
     *
     * _Available since v3.4._
     */
    function tryDiv(uint256 a, uint256 b) internal pure returns (bool, uint256) {
        if (b == 0) return (false, 0);
        return (true, a / b);
    }

    /**
     * @dev Returns the remainder of dividing two unsigned integers, with a division by zero flag.
     *
     * _Available since v3.4._
     */
    function tryMod(uint256 a, uint256 b) internal pure returns (bool, uint256) {
        if (b == 0) return (false, 0);
        return (true, a % b);
    }

    /**
     * @dev Returns the addition of two unsigned integers, reverting on
     * overflow.
     *
     * Counterpart to Solidity's `+` operator.
     *
     * Requirements:
     *
     * - Addition cannot overflow.
     */
    function add(uint256 a, uint256 b) internal pure returns (uint256) {
        uint256 c = a + b;
        require(c >= a, "SafeMath: addition overflow");
        return c;
    }

    /**
     * @dev Returns the subtraction of two unsigned integers, reverting on
     * overflow (when the result is negative).
     *
     * Counterpart to Solidity's `-` operator.
     *
     * Requirements:
     *
     * - Subtraction cannot overflow.
     */
    function sub(uint256 a, uint256 b) internal pure returns (uint256) {
        require(b <= a, "SafeMath: subtraction overflow");
        return a - b;
    }

    /**
     * @dev Returns the multiplication of two unsigned integers, reverting on
     * overflow.
     *
     * Counterpart to Solidity's `*` operator.
     *
     * Requirements:
     *
     * - Multiplication cannot overflow.
     */
    function mul(uint256 a, uint256 b) internal pure returns (uint256) {
        if (a == 0) return 0;
        uint256 c = a * b;
        require(c / a == b, "SafeMath: multiplication overflow");
        return c;
    }

    /**
     * @dev Returns the integer division of two unsigned integers, reverting on
     * division by zero. The result is rounded towards zero.
     *
     * Counterpart to Solidity's `/` operator. Note: this function uses a
     * `revert` opcode (which leaves remaining gas untouched) while Solidity
     * uses an invalid opcode to revert (consuming all remaining gas).
     *
     * Requirements:
     *
     * - The divisor cannot be zero.
     */
    function div(uint256 a, uint256 b) internal pure returns (uint256) {
        require(b > 0, "SafeMath: division by zero");
        return a / b;
    }

    /**
     * @dev Returns the remainder of dividing two unsigned integers. (unsigned integer modulo),
     * reverting when dividing by zero.
     *
     * Counterpart to Solidity's `%` operator. This function uses a `revert`
     * opcode (which leaves remaining gas untouched) while Solidity uses an
     * invalid opcode to revert (consuming all remaining gas).
     *
     * Requirements:
     *
     * - The divisor cannot be zero.
     */
    function mod(uint256 a, uint256 b) internal pure returns (uint256) {
        require(b > 0, "SafeMath: modulo by zero");
        return a % b;
    }

    /**
     * @dev Returns the subtraction of two unsigned integers, reverting with custom message on
     * overflow (when the result is negative).
     *
     * CAUTION: This function is deprecated because it requires allocating memory for the error
     * message unnecessarily. For custom revert reasons use {trySub}.
     *
     * Counterpart to Solidity's `-` operator.
     *
     * Requirements:
     *
     * - Subtraction cannot overflow.
     */
    function sub(uint256 a, uint256 b, string memory errorMessage) internal pure returns (uint256) {
        require(b <= a, errorMessage);
        return a - b;
    }

    /**
     * @dev Returns the integer division of two unsigned integers, reverting with custom message on
     * division by zero. The result is rounded towards zero.
     *
     * CAUTION: This function is deprecated because it requires allocating memory for the error
     * message unnecessarily. For custom revert reasons use {tryDiv}.
     *
     * Counterpart to Solidity's `/` operator. Note: this function uses a
     * `revert` opcode (which leaves remaining gas untouched) while Solidity
     * uses an invalid opcode to revert (consuming all remaining gas).
     *
     * Requirements:
     *
     * - The divisor cannot be zero.
     */
    function div(uint256 a, uint256 b, string memory errorMessage) internal pure returns (uint256) {
        require(b > 0, errorMessage);
        return a / b;
    }

    /**
     * @dev Returns the remainder of dividing two unsigned integers. (unsigned integer modulo),
     * reverting with custom message when dividing by zero.
     *
     * CAUTION: This function is deprecated because it requires allocating memory for the error
     * message unnecessarily. For custom revert reasons use {tryMod}.
     *
     * Counterpart to Solidity's `%` operator. This function uses a `revert`
     * opcode (which leaves remaining gas untouched) while Solidity uses an
     * invalid opcode to revert (consuming all remaining gas).
     *
     * Requirements:
     *
     * - The divisor cannot be zero.
     */
    function mod(uint256 a, uint256 b, string memory errorMessage) internal pure returns (uint256) {
        require(b > 0, errorMessage);
        return a % b;
    }
}

// SPDX-License-Identifier: MIT

pragma solidity >=0.6.0 <0.8.0;

/*
 * @dev Provides information about the current execution context, including the
 * sender of the transaction and its data. While these are generally available
 * via msg.sender and msg.data, they should not be accessed in such a direct
 * manner, since when dealing with GSN meta-transactions the account sending and
 * paying for execution may not be the actual sender (as far as an application
 * is concerned).
 *
 * This contract is only required for intermediate, library-like contracts.
 */
abstract contract Context {
    function _msgSender() internal view virtual returns (address payable) {
        return msg.sender;
    }

    function _msgData() internal view virtual returns (bytes memory) {
        this; // silence state mutability warning without generating bytecode - see https://github.com/ethereum/solidity/issues/2691
        return msg.data;
    }
}

// SPDX-License-Identifier: MIT
pragma solidity >=0.6.0 <0.8.0;
import "@chainlink/contracts/src/v0.7/VRFConsumerBase.sol";
import "@openzeppelin/contracts/access/Ownable.sol";
import "@openzeppelin/contracts/math/SafeMath.sol";

import "./interfaces/IRFOXCollectible.sol";

contract RFOXLootBox is VRFConsumerBase, Ownable {
    using SafeMath for uint256;

    // Configuration for Chainlink Randomness
    bytes32 internal keyHash;
    uint256 internal chainlinkFee;
    IRFOXCollectible RFOXCollectible;

    // Public enum class
    enum Class {
        Common,
        Uncommon,
        Rare,
        UltraRare,
        Slammer,
        SlammerGold
    }
    // Number of existence classes
    uint256 constant NUM_CLASSES = 6;
    // Basis point constants
    uint256 constant INVERSE_BASIS_POINT = 10000;
    // RFOX Packages
    enum Pack {
        Basic,
        Standard,
        Premium,
        Elite,
        Event
    }
    // Number of existence package
    uint256 constant NUM_PACKS = 5;
    // Package setting for package
    // Price of package, number of draws,...
    struct PackSettings {
        // Price of each pack
        uint256 price;
        // Number of items to send per open.
        // Set to 0 to disable this Option.
        uint256 numberOfDraws;
        // Probability in basis points (out of 10,000) of receiving each class (descending)
        uint16[NUM_CLASSES] classProbabilities;
        // Whether to enable `guarantees` below
        bool hasGuaranteedClasses;
        // Number of items you're guaranteed to get, for each class
        uint16[NUM_CLASSES] guarantees;
    }
    // Request for open package
    // Request are fulfilled by Chainlink VRF
    struct Request {
        address owner;
        bytes32 requestId;
        Pack pack;
        uint256 timestamp;
    }
    // From enum to get Package Settings
    mapping(uint256 => PackSettings) packToSettings;
    // User seed for each random
    // Make more difficult for predicting numbers
    mapping(address => uint256) userSeed;
    // From Request ID to get User's request
    mapping(bytes32 => Request) public userRequests;
    // From Request ID to get Random number by chainlink
    mapping(bytes32 => uint256) public requestIdToRandomNumber;
    // Mapping from class to array of IDS
    mapping(Class => uint256[]) classToIds;

    // Modifier that check if pack are enabled
    modifier packEnabled(Pack _pack) {
        PackSettings memory pack = packToSettings[uint256(_pack)];
        require(
            pack.numberOfDraws > 0,
            "RFOXLootbox: Pack not exist or disabled"
        );
        _;
    }

    // Modifier that check if class is exists
    modifier classExists(Class _class) {
        require(
            uint8(_class) < NUM_CLASSES,
            "RFOXLootbox: Class out of bounds"
        );
        _;
    }

    // Modifier that check if class is exists
    modifier classHasCards(Class _class) {
        require(
            classToIds[_class].length > 0,
            "RFOXLootbox: Class empty cards"
        );
        _;
    }

    event ClassCardIdsChanged(Class class, uint256[] newIds);
    event PackOpened(address indexed opener, bytes32 requestId);
    event PackFulfilled(address indexed opener, bytes32 requestId, Pack pack);
    event NewPackAdded(
        Pack indexed pack,
        uint256 price,
        uint256 draws,
        uint16[NUM_CLASSES] classProbabilities,
        uint16[NUM_CLASSES] guarantees
    );

    fallback() external {
        revert("No function matches");
    }

    receive() external payable {}

    /**
     * Constructor inherits VRFConsumerBase
     * 
     * Network: BSC Mainnet
     * Chainlink VRF Coordinator address: 0x747973a5A2a4Ae1D3a8fDF5479f1514F65Db9C31
     * LINK token address:                0x404460C6A5EdE2D891e8297795264fDe62ADBB75
     * Key Hash: 0xc251acd21ec4fb7f31bb8868288bfdbaeb4fbfec2df3735ddbd4f7dc8d60103c
     * fee = 0.2 * 10 ** 18;

     * Network: BSC Testnet
     * Chainlink VRF Coordinator address: 0xa555fC018435bef5A13C6c6870a9d4C11DEC329C
     * LINK token address:                0x84b9B910527Ad5C03A9Ca831909E21e236EA7b06
     * Key Hash: 0xcaf3c3727e033261d383b315559476f48034c13b18f8cafed4d871abe5049186
     * fee = 0.1 * 10 ** 18;

     * Network: Rinkeby Testnet
     * Chainlink VRF Coordinator address: 0xb3dCcb4Cf7a26f6cf6B120Cf5A73875B7BBc655B
     * LINK token address:                0x01BE23585060835E02B77ef475b0Cc51aA1e0709
     * Key Hash: 0x2ed0feb3e7fd2022120aa84fab1945545a9f2ffc9076fd6156fa96eaff4c1311
     * fee = 0.1 * 10 ** 18;
     */
    constructor(address _RFOXCollectible)
        VRFConsumerBase(
            0xb3dCcb4Cf7a26f6cf6B120Cf5A73875B7BBc655B,
            0x01BE23585060835E02B77ef475b0Cc51aA1e0709
        )
    {
        RFOXCollectible = IRFOXCollectible(_RFOXCollectible);
        // Setup chainlink VRF for using
        // True random
        keyHash = 0x2ed0feb3e7fd2022120aa84fab1945545a9f2ffc9076fd6156fa96eaff4c1311;
        chainlinkFee = 0.1 * 10**18;
    }

    /// @notice Open 1 pack
    /// @dev Open pack will use PackSetting to use
    /// @param _pack Pack number
    function openPack(Pack _pack) external payable packEnabled(_pack) {
        PackSettings memory pack = packToSettings[uint256(_pack)];
        require(msg.value >= pack.price, "RFOXLootbox: Underprice");

        bytes32 requestId = getRandomNumber();
        userRequests[requestId] = Request({
            owner: msg.sender,
            requestId: requestId,
            pack: _pack,
            timestamp: block.timestamp
        });

        emit PackOpened(msg.sender, requestId);
    }

    /// @notice Claim pack fee from user for building platform
    /// 100 % boxes fee will be reinvest and building RFOX platfomr
    /// @dev Only owner can claim
    /// @param _amount Amount of boxes fee
    function claimBoxFee(uint256 _amount) external onlyOwner {
        require(
            address(this).balance >= _amount,
            "RFOXLootbox: Amount exceed balance"
        );
        msg.sender.transfer(_amount);
    }

    /// @notice Admin create new set of cards
    /// @dev This function call to RFOXCollectible ERC1155
    /// @param _class Class type for new card
    function createCard(Class _class) public onlyOwner classExists(_class) {
        uint256 cardId = RFOXCollectible.create(msg.sender, 0, "");
        classToIds[_class].push(cardId);
    }

    /// @notice Get card ids by class
    /// @dev Class in range [0:NUM_CLASSES]
    /// @param _class Class number
    /// @return cards Array of card ids
    function getCardIdByClass(Class _class)
        public
        view
        classExists(_class)
        returns (uint256[] memory cards)
    {
        return classToIds[_class];
    }

    /// @notice Force remove id from classes
    /// @dev Only Owner can remove card id to each class. Duplicate ids is not allowed
    /// @param _class Class of new card
    /// @param _cardId Id of card
    function changeClassCardIds(Class _class, uint256[] memory _cardId)
        public
        classExists(_class)
        onlyOwner
    {
        classToIds[_class] = _cardId;

        emit ClassCardIdsChanged(_class, _cardId);
    }

    /// @notice Admin setting pack configurations. _classProbabilities MUST has sum of 10000
    /// @dev hasGuaranteedClasses are auto calculated.
    /// @param _pack Option for which packs
    /// @param _price Price of this Pack
    /// @param _numberOfDraws Number of cards can draw each time open box
    /// @param _classProbabilities Draw rate of each classes
    /// @param _guarantees Guarantee cards each draws
    function addNewPack(
        Pack _pack,
        uint256 _price,
        uint256 _numberOfDraws,
        uint16[NUM_CLASSES] memory _classProbabilities,
        uint16[NUM_CLASSES] memory _guarantees
    ) public onlyOwner {
        bool hasGuaranteed = false;

        for (uint256 i = 0; i < _guarantees.length; i++) {
            if (_guarantees[i] != 0) {
                hasGuaranteed = true;
            }
        }

        packToSettings[uint256(_pack)] = PackSettings({
            price: _price,
            numberOfDraws: _numberOfDraws,
            classProbabilities: _classProbabilities,
            hasGuaranteedClasses: hasGuaranteed,
            guarantees: _guarantees
        });

        emit NewPackAdded(_pack, _price, _numberOfDraws, _classProbabilities, _guarantees);
    }

    /// @notice Return the pack setting of _pack
    /// @dev Pack in range [0:NUM_PACKS] are valid
    /// @param _pack Uint pack enum
    /// @return price Price of the pack
    /// @return numberOfDraws Number of cards can receive
    /// @return classProbabilities Rate of each class
    /// @return hasGuaranteedClasses Is guaranteed any class
    /// @return guarantees Array of guarantee classes
    function getPackSetting(Pack _pack)
        public
        view
        returns (
            uint256 price,
            uint256 numberOfDraws,
            uint16[NUM_CLASSES] memory classProbabilities,
            bool hasGuaranteedClasses,
            uint16[NUM_CLASSES] memory guarantees
        )
    {
        PackSettings memory pack = packToSettings[uint256(_pack)];
        return (
            pack.price,
            pack.numberOfDraws,
            pack.classProbabilities,
            pack.hasGuaranteedClasses,
            pack.guarantees
        );
    }

    function getRequestOwner(bytes32 _requestId)
        public
        view
        returns (
            address owner,
            bytes32 requestId,
            Pack pack,
            uint256 timestamp
        )
    {
        Request memory request = userRequests[_requestId];
        return (
            request.owner,
            request.requestId,
            request.pack,
            request.timestamp
        );
    }

    /// @notice Function that expand one random number to multple numbers
    /// @dev For information, see https://docs.chain.link/docs/chainlink-vrf-best-practices/
    /// @param _randomValue Random number, always come from VRF using truly data
    /// @param _n Number of expand values that return in array
    /// @param _seed Generate different seeds for each random
    /// @return expandedValues Uint array of random numbers generated from 1 random number.
    function expand(
        uint256 _randomValue,
        uint256 _n,
        uint256 _seed
    ) internal view returns (uint256[] memory expandedValues) {
        expandedValues = new uint256[](_n);
        for (uint256 i = 0; i < _n; i++) {
            expandedValues[i] = uint256(
                keccak256(
                    abi.encode(
                        block.timestamp,
                        block.difficulty,
                        _randomValue,
                        i,
                        _seed
                    )
                )
            );
            _seed = _seed.add(expandedValues[i].mod(NUM_CLASSES));
        }
        return expandedValues;
    }

    /**
     * Requests randomness
     */
    function getRandomNumber() internal returns (bytes32 requestId) {
        require(
            LINK.balanceOf(address(this)) >= chainlinkFee,
            "Not enough LINK - fill contract with faucet"
        );
        return requestRandomness(keyHash, chainlinkFee);
    }

    /**
     * Callback function used by VRF Coordinator
     */
    function fulfillRandomness(bytes32 _requestId, uint256 _randomness)
        internal
        override
    {
        (address owner, bytes32 requestId, Pack pack, ) = getRequestOwner(
            _requestId
        );
        requestIdToRandomNumber[requestId] = _randomness;
        userSeed[owner] = userSeed[owner].add(_randomness.mod(NUM_CLASSES));

        openRandomCards(owner, _randomness, pack);

        emit PackFulfilled(owner, requestId, pack);
    }

    function openRandomCards(
        address _owner,
        uint256 _randomness,
        Pack _pack
    ) internal {
        // Array of card ids. Class to array of ids
        uint256[] memory randomNumbers;
        uint256[] memory cardIds;
        uint256 minted = 0;
        uint256 seed = userSeed[_owner];
        // Get pack settings
        PackSettings memory packSettings = packToSettings[uint256(_pack)];

        (
            ,
            uint256 numberOfDraws,
            uint16[NUM_CLASSES] memory classProbabilities,
            bool hasGuaranteedClasses,
            uint16[NUM_CLASSES] memory guarantees
        ) = getPackSetting(_pack);

        for (uint256 i = 0; i < guarantees.length; i++) {
            if (guarantees[i] > 0) {
                randomNumbers = pickRandomIn(_randomness, guarantees[i], i);
                seed += i;
                cardIds = getRandomCardIdsByClass(Class(i), randomNumbers);
                minted += cardIds.length;
                RFOXCollectible.mintOneBatch(_owner, cardIds, "");
            }
        }

        uint256 remainCards = numberOfDraws.sub(minted);

        if (remainCards > 0) {
            uint256[] memory remainCardIds = new uint256[](remainCards);
            randomNumbers = pickRandomIn(_randomness, remainCards, seed);

            for (uint256 i = 0; i < remainCards; i++) {
                uint256 randomNumber = randomNumbers[i];
                Class cardClass = getClassByProbabilities(
                    randomNumber,
                    classProbabilities
                );
                remainCardIds[i] = getRandomCardIdByClass(
                    cardClass,
                    randomNumber
                );
                minted += 1;
            }

            RFOXCollectible.mintOneBatch(_owner, remainCardIds, "");
        }
    }

    function pickRandomIn(
        uint256 _randomness,
        uint256 _numbers,
        uint256 _seed
    ) internal view returns (uint256[] memory randomResults) {
        randomResults = expand(
            _randomness,
            _numbers,
            _randomness.mod(_seed.add(NUM_CLASSES))
        );
    }

    function getRandomCardIdsByClass(Class _class, uint256[] memory _numbers)
        internal
        view
        classExists(_class)
        classHasCards(_class)
        returns (uint256[] memory ids)
    {
        ids = new uint256[](_numbers.length);

        for (uint256 i = 0; i < _numbers.length; i++) {
            uint256 index = _numbers[i].mod(classToIds[_class].length);
            ids[i] = classToIds[_class][index];
        }

        return ids;
    }

    function getRandomCardIdByClass(Class _class, uint256 _number)
        internal
        view
        classExists(_class)
        classHasCards(_class)
        returns (uint256 ids)
    {
        uint256 index = _number.mod(classToIds[_class].length);
        return classToIds[_class][index];
    }

    function getClassByProbabilities(
        uint256 _random,
        uint16[NUM_CLASSES] memory _classProbabilities
    ) internal pure returns (Class _class) {
        uint256 value = _random.mod(INVERSE_BASIS_POINT);
        for (uint256 i = _classProbabilities.length - 1; i > 0; i--) {
            uint256 probability = _classProbabilities[i];
            if (value < probability) {
                return Class(i);
            } else {
                value = value.sub(probability);
            }
        }

        return Class.Common;
    }
}

// SPDX-License-Identifier: MIT

pragma solidity >=0.6.2 <0.8.0;
/**
 * @dev Required interface of an ERC1155 compliant contract, as defined in the
 * https://eips.ethereum.org/EIPS/eip-1155[EIP].
 *
 * _Available since v3.1._
 */
interface IRFOXCollectible {
    /**
     * @dev Emitted when `value` tokens of token type `id` are transferred from `from` to `to` by `operator`.
     */
    event TransferSingle(address indexed operator, address indexed from, address indexed to, uint256 id, uint256 value);

    /**
     * @dev Equivalent to multiple {TransferSingle} events, where `operator`, `from` and `to` are the same for all
     * transfers.
     */
    event TransferBatch(address indexed operator, address indexed from, address indexed to, uint256[] ids, uint256[] values);

    /**
     * @dev Emitted when `account` grants or revokes permission to `operator` to transfer their tokens, according to
     * `approved`.
     */
    event ApprovalForAll(address indexed account, address indexed operator, bool approved);

    /**
     * @dev Emitted when the URI for token type `id` changes to `value`, if it is a non-programmatic URI.
     *
     * If an {URI} event was emitted for `id`, the standard
     * https://eips.ethereum.org/EIPS/eip-1155#metadata-extensions[guarantees] that `value` will equal the value
     * returned by {IERC1155MetadataURI-uri}.
     */
    event URI(string value, uint256 indexed id);

    /**
     * @dev Returns the amount of tokens of token type `id` owned by `account`.
     *
     * Requirements:
     *
     * - `account` cannot be the zero address.
     */
    function balanceOf(address account, uint256 id) external view returns (uint256);

    /**
     * @dev xref:ROOT:erc1155.adoc#batch-operations[Batched] version of {balanceOf}.
     *
     * Requirements:
     *
     * - `accounts` and `ids` must have the same length.
     */
    function balanceOfBatch(address[] calldata accounts, uint256[] calldata ids) external view returns (uint256[] memory);

    /**
     * @dev Grants or revokes permission to `operator` to transfer the caller's tokens, according to `approved`,
     *
     * Emits an {ApprovalForAll} event.
     *
     * Requirements:
     *
     * - `operator` cannot be the caller.
     */
    function setApprovalForAll(address operator, bool approved) external;

    /**
     * @dev Returns true if `operator` is approved to transfer ``account``'s tokens.
     *
     * See {setApprovalForAll}.
     */
    function isApprovedForAll(address account, address operator) external view returns (bool);

    /**
     * @dev Transfers `amount` tokens of token type `id` from `from` to `to`.
     *
     * Emits a {TransferSingle} event.
     *
     * Requirements:
     *
     * - `to` cannot be the zero address.
     * - If the caller is not `from`, it must be have been approved to spend ``from``'s tokens via {setApprovalForAll}.
     * - `from` must have a balance of tokens of type `id` of at least `amount`.
     * - If `to` refers to a smart contract, it must implement {IERC1155Receiver-onERC1155Received} and return the
     * acceptance magic value.
     */
    function safeTransferFrom(address from, address to, uint256 id, uint256 amount, bytes calldata data) external;

    /**
     * @dev xref:ROOT:erc1155.adoc#batch-operations[Batched] version of {safeTransferFrom}.
     *
     * Emits a {TransferBatch} event.
     *
     * Requirements:
     *
     * - `ids` and `amounts` must have the same length.
     * - If `to` refers to a smart contract, it must implement {IERC1155Receiver-onERC1155BatchReceived} and return the
     * acceptance magic value.
     */
    function safeBatchTransferFrom(address from, address to, uint256[] calldata ids, uint256[] calldata amounts, bytes calldata data) external;
     /**
     * @dev Returns the URI for token type `id`.
     *
     * If the `\{id\}` substring is present in the URI, it must be replaced by
     * clients with the actual token type ID.
     */
    function uri(uint256 id) external view returns (string memory);
    /**
     * @dev Create new token on ERC1155 contract
     * emit URI(string value, uint256 indexed id) event
     * @param _owner Owner of tokens
     * @param _initialSupply Initial Supply amount for this token
     * @param _data Data field for create token (Optional by `${""}`)
     * @return Token ID of new token
     */
    function create(address _owner, uint256 _initialSupply, bytes calldata _data) external returns (uint256);

    /**
     * @dev Create multiple token on ERC1155 contract
     * emit URI(string value, uint256 indexed id) event each token created
     * @param _owners List Owner address of tokens
     * @param _initialSupplies List Initial Supply amount for tokens
     * @return List token IDs of new tokens
     */
    function createBatch(address[] memory _owners, uint256[] memory _initialSupplies) external returns (uint256[] memory);

    /**
     * @dev Mint batch token 
     * emit URI(string value, uint256 indexed id) event each token created
     * @param _to Token recipient address
     * @param _ids List of token IDs that send to recipient
     * @param _amounts Amount of tokens
     * @param _data Optinal external data
     */
    function mintBatch(address _to, uint256[] memory _ids, uint256[] memory _amounts, bytes memory _data) external;

    /**
     * @dev Mint batch token. One each ID
     * emit URI(string value, uint256 indexed id) event each token created
     * @param _to Token recipient address
     * @param _ids List of token IDs that send to recipient
     * @param _data Optinal external data
     */
    function mintOneBatch(
        address _to,
        uint256[] memory _ids,
        bytes memory _data
    ) external;
}

